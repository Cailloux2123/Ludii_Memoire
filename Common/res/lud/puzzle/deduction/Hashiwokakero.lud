(game "Hashiwokakero" 
    (players 1) 
    (equipment {
        (board <Challenge:type> (values Edge (range 1 2)) use:Edge) 
        (hints { <Challenge:hints> } vertex)
    }) 
    (rules
        (play 
            (satisfy {
                //
                (forAll Vertex 
                    (or
                        (is Sum Edge (sites Incident Edge of:Vertex at:(from)) (hint Vertex at:(from)))
                        (= (hint Vertex at:(from)) Off) 
                    )
                )
                (not (is Crossed))
                (< (count Groups Edge) 2)
            })
        )
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{ <type> <hints> }
    {  
    (item "Nikoli" 
        <
        (graph
            vertices:{ {2 0} {6 0} {10 0} 
            {8 2} {12 2} 
            {0 4} {6 4} {10 4} 
            {0 8} {6 8} {12 8}
            {2 10} {10 10} 
            {0 12} {4 12} {8 12} {12 12}}
            edges:{{0 1} {0 11} {1 2} {1 6} {2 7} {3 4} {3 15} {4 10} {5 8} {5 6} {6 7} {6 9} {7 12} {8 9} {8 13} 
            {9 10} {10 16} {11 12} {13 14} {14 15} {15 16}}
        )
        >
        <
        (hint 0 1) 
        (hint 1 4) 
        (hint 2 1) 
        (hint 3 1) 
        (hint 4 3) 
        (hint 5 2) 
        (hint 6 8) 
        (hint 7 4)
        (hint 8 3)
        (hint 9 3)
        (hint 10 3)
        (hint 11 2)
        (hint 12 4)
        (hint 13 4)
        (hint 14 3)
        (hint 15 3)
        (hint 16 3)
        >
        ""
    )
})

//------------------------------------------------------------------------------

(metadata
    (info
        {
        (description "Hashiwokakero is played on a rectangular grid of no standard size. There are numbers from 1 to 8 inclusive. They are usually circled and called islands. The aim of the game is to link all the islands into a single group by creating a series of bridges between them.")
        (aliases {"Bridges" "Hashi" "Archipel" "Connect"})
        (rules "Connect islands (the circles with numbers) with as many bridges as the number in the island. There can be no more than two bridges between two islands. Bridges cannot go across islands or other bridges. The bridges will form a continuous link between all the islands.")
        (source "<a href=\"https://www.nikoli.co.jp/en/puzzles/hashiwokakero\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Nikoli</a>")
        (version "1.3.12")
        (classification "puzzle/deduction")
        (publisher "Nikoli")
        (credit "Pierre Accou & Tom Doumont")
        }
    )
    (graphics {
        (board Style PenAndPaper replaceComponentsWithFilledCells:True)
        (player Colour P1 (colour Red))
        
    })
)
