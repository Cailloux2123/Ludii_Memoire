(game "Hexagonal Nonogram" 
    (players 1) 
    (equipment { 
        (board (hex <Challenge:size>) (values Cell (range 0 1)))
        <Challenge:hints>
        (regions HintRegions) 
        (piece "Hex" P1)
    }) 
    (rules
        (play (satisfy {
        	    (is Match 0)
            })
        )
        (end (if (is Solved) (result P1 Win)))
    )
)

//------------------------------------------------------------------------------

(option "Challenge" <Challenge> args:{<size> <hints>}
    {
    (item "3" 
        <3>
        <
        (hints 
        	{
	        //Row
	        (hint {7 8 9 10 11} {2 2})
	        (hint {0 1 2} {2})
	        (hint {3 4 5 6} {1 1})
	        (hint {12 13 14 15} {1 1})
	        (hint {16 17 18} {2})
	        
	        //Column
	        (hint {0 3 7} {3})
	        (hint {1 4 8 12} {1 2})
	        (hint {2 5 9 13 16} {1 1})
	        (hint {6 10 14 17} {3})
	        (hint {11 15 18} {1})
	        
	        //Column
	        (hint {16 12 7} {3})
	        (hint {17 13 8 3} {1 2})
	        (hint {18 14 9 4 0} {1 1})
	        (hint {15 10 5 1} {3})
	        (hint {11 6 2} {1})
        	}
        )>
        ""
    )
    
})

//------------------------------------------

(metadata    
	(info
		    {
		        (description "It's a variant of Nonogram where the board is now a hexagon")
		        (rules "You use digits to create a pattern of filled-in hexagon in the empty grid provided.  Each number on the lines outside the grid represents a block of hexagon to be blacked out in 3 directions : W, SE or NE.")
		        (source "<a href=\"https://fr.wikipedia.org/wiki/Picross\" target=\"_blank\" class=\"style1\" style=\"color: #0000EE\" />Wikipedia</a>")
		        (version "1.3.12")
		        (classification "puzzle/deduction")
		        (credit "Pierre Accou")
		    }
		)
	
	(graphics {
		(piece Rename piece:"1" "Hex")
		(board Placement scale:0.60)
		(drawHint NextTo)
	})
)
